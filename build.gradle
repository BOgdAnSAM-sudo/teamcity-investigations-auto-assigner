plugins {
  id 'com.github.rodm.teamcity-server' version '1.4.1'
  id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'idea'

ext {
  teamcityVersion = project.hasProperty('teamcityVersion') ? property('teamcityVersion') : '2022.02'
  versionNumber = project.hasProperty('versionNumber') ? property('versionNumber') : 'SNAPSHOT-' + new Date().format('yyyyMMddHHmmss')
}

group = 'jetbrains.buildServer.investigationsAutoAssigner'

version = versionNumber

/*
 * Copyright 2000-2022 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Remove repositories added by plugins
project.plugins.withType(JavaPlugin) {
  project.repositories.clear()
}

repositories {
  maven { url "https://download.jetbrains.com/teamcity-repository" }
  mavenCentral()
  mavenLocal()
}

configurations {
}

dependencies {
  provided(group: 'org.jetbrains.teamcity.internal', name: 'server', version: "${teamcityVersion}")
  testCompile 'org.mockito:mockito-core:2.18.0'
  testCompile 'org.assertj:assertj-core:2.2.0'
  testCompile 'com.google.jimfs:jimfs:1.1'
  testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  testCompile 'org.awaitility:awaitility:3.1.6'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

test {
  useTestNG()
}

teamcity {
  version = teamcityVersion
  allowSnapshotVersions = true

  server {
    descriptor {
      name = project.name
      displayName = 'Investigations Auto-Assigner'
      version = project.version
      vendorName = 'JetBrains'
      vendorUrl = 'http://www.jetbrains.com/'
      description = 'Automatically assigns investigations according to a set of rules.'

      useSeparateClassloader = true
      nodeResponsibilitiesAware = true

      files {
        into('kotlin-dsl') {
          from("${rootProject.projectDir}/kotlin-dsl")
        }
      }
    }
  }

}

// Do not include version into plugin archive name
serverPlugin.version = null

idea {
  module {
    downloadJavadoc = false
    downloadSources = true
  }
}


compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}